buildscript {

    ext {
        nexus = {
            credentials {
                username nexusBuildUserToken
                password nexusBuildPassToken
            }
            url nexusRepoURL
        }
    }

    repositories {
        maven(nexus)
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${dependencyVersionSpringBootGradlePlugin}")
        classpath("net.researchgate:gradle-release:${dependencyVersionGradleReleasePlugin}")
    }
}

repositories {
    maven(nexus)
}

// IDE plugins
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'

// Java dev plugins
apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'jdepend'
apply plugin: 'spring-boot'
apply plugin: 'maven'

// code quality plugins
apply plugin: 'checkstyle'
apply plugin: 'sonar-runner'

// release plugins
apply plugin: 'net.researchgate.release'

// project artifact info
group = groupId
archivesBaseName = artifactId

// configure plugins

sonarRunner {
    sonarProperties {
        property 'sonar.projectKey', "${group}:${archivesBaseName}"
        property 'sonar.projectName', "Microservice - ${archivesBaseName}"
        property 'sonar.projectVersion', version
        property "sonar.host.url", sonarHostURL
        property "sonar.jdbc.url", sonarJDBCURL
        property "sonar.jdbc.username", sonarJDBCUsername
        property "sonar.jdbc.password", sonarJDBCPassword
        property 'sonar.language', 'java'
        property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/jacoco.exec"
        // property 'sonar.exclusions', 'au/com/auspost/project/resources/**/*'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: nexusReleaseURL) {
                authentication(userName: nexusBuildUserToken, password: nexusBuildPassToken);
            }
            snapshotRepository(url: nexusSnapshotURL) {
                authentication(userName: nexusBuildUserToken, password: nexusBuildPassToken);
            }
        }
    }
}

jacoco {
    toolVersion = "${dependencyVersionJacocoTool}"
    reportsDir = file("${buildDir}/jacocoReportDir")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

release {
    git {
        requireBranch = /master|integration.*/
        pushToCurrentBranch = true
    }
}


checkstyle {
    toolVersion = "6.0"
}

// only tag a release if the upload of the artefacts to Nexus was successful
createReleaseTag.dependsOn uploadArchives

// configure project specific properties/dependencies

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Add a provided configuration.
// This is used to add jar dependencies that are needed for compile time only
// but should not be bundled in the war/ear projects.
configurations { provided }
sourceSets.main.compileClasspath += [configurations.provided]
sourceSets.test.compileClasspath += [configurations.provided]
sourceSets.test.runtimeClasspath += [configurations.provided]
// optional: if using 'eclipse' plugin
eclipse.classpath.plusConfigurations += [configurations.provided]

dependencies {
    // web dependencies
    // compile("au.com.auspost.boot:auspost-spring-boot-starter-web:${dependencyVersionAuspostSpringBoot}")

    // service monitoring and stats (exposed via /_admin context path)
    // compile("au.com.auspost.boot:auspost-spring-boot-starter-actuator:${dependencyVersionAuspostSpringBoot}")

    // swagger documentation
    // compile("au.com.auspost.boot:auspost-spring-boot-starter-swagger:${dependencyVersionAuspostSpringBoot}")

    // database dependencies
    // compile("au.com.auspost.boot:auspost-spring-boot-starter-data-jpa:${dependencyVersionAuspostSpringBoot}")

    compile 'org.springframework.boot:spring-boot-starter:1.2.7.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.2.7.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:1.2.7.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security:1.2.7.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf:1.2.7.RELEASE'

    compile 'com.h2database:h2:1.4.190'

    compile 'mysql:mysql-connector-java'

    // model and dto object mapper
    compile 'org.modelmapper:modelmapper:0.7.4'

    // webjars.org client side libraries into JAR files
    compile 'org.webjars:react:0.14.0'
    compile 'org.webjars:jquery:1.11.3'

    // test dependencies
    testCompile("org.springframework.boot:spring-boot-starter-test:${dependencyVersionSpringBoot}")
}
